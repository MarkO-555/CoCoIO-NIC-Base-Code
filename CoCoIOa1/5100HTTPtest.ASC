0 REM KEEP THIS LINE FOR TOOLSHED !!!!
5 REM *** 4/14/21 DLINK ROUTER AS GATEWAY, LAPTOP AS WEB SERVER ***
10 REM FIXED BUFFER ADDR/MASK ASSUMES SOCKET 0 ONLY, WITH 2k BUFFER
100 WIDTH 80
110 CLS
115 C=1
120 PRINT "** TEST 5100 HTTP SOCKET V0.2 **" 
130 POKE &HFF68,&H80   'RESET 5100
140 POKE &HFF68,&H03   'AUTO:INC, INDIRECT MODE
1100 ' ***** LOCAL CONFIG *****
1110 POKE &HFF69,0 : POKE &HFF6A,01                        ' START AUTOINC HERE
1120 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,1   'GATEWAY
1130 POKE &HFF6B,255 : POKE &HFF6B,255 : POKE &HFF6B,255 : POKE &HFF6B,0 'SUBNET
1140 POKE &HFF6B,&H5C : POKE &HFF6B,&H26 : POKE &HFF6B,&H0A              'MAC ADDR PT1
1150 POKE &HFF6B,&H01 : POKE &HFF6B,&H02 : POKE &HFF6B,&H03              'MAC ADDR PT2
1160 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,7   'MY IP
1200 ' **** REMOTE CONFIG *****
1210 POKE &HFF69,&H04 : POKE &HFF6A,&H00 : POKE &HFF6B, 1  ' S0 > TCP MODE
1220 POKE &HFF69,&H04 : POKE &HFF6A,&H04                   ' LOCAL PORT 
1230 POKE &HFF6B,&H80 : POKE &HFF6B,RND(&HFF)              '  EPHM PORT #
1240 POKE &HFF69,&H04 : POKE &HFF6A,&H0C                   ' FOREIGN IP 
1250 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,124
1260 POKE &HFF6B,0 : POKE &HFF6B,80                        '  HTTP PORT # (AUTOINC)
1270 RM=1999 : TM=1999                                     '  MASK ASSUMES 2K BUFFER
1300 REM *** OPEN SOCKET ******************************
1310 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B, 1  ' TRY TO OPEN SOCKET
1320 POKE &HFF69,&H04 : POKE &HFF6A,&H03 : S = PEEK(&HFF6B)' GET STATUS
1330 IF S <> &H13 THEN 20040                               ' STAT <> SOCK_INIT
1340 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B, 4  ' TRY CONNECT   
1350 POKE &HFF69,&H04 : POKE &HFF6A,&H03 : C = PEEK(&HFF6B)' GET STATUS
1360 IF C = 0 THEN 20030                                   ' STAT = SOCK_CLOSED
1370 IF C <> &H17 THEN 1320                                ' DO UNTIL SOCK_ESTAB 
1400 REM *** TICKLE THE WEB SERVER ********************
1405 GOTO 1490 ' BYPASS 
1410 WP = &H6000                                           ' START WRITE BUFFER     
1420 B = INT(WP/256) : C = WP-(256*B)
1430 POKE &HFF69,B : POKE &HFF6A,C 
1440 POKE &HFF6B,&H0D : POKE &HFF6B,&H0A                   ' CR + LF 
1490 REM **********************************************  
1500 PRINT : PRINT "WAITING FOR DATA";   
1510 POKE &HFF69,&H04 : POKE &HFF6A,&H26
1520 RZ = (PEEK(&HFF6B)*256)+PEEK(&HFF6B)                  ' RZ = READ SIZE
1530 PRINT ".";
1540 IF RZ = 0 THEN 1510                                   ' DO UNTIL DATA EXISTS
1550 REM *** START DATA READ **************************    RICK: SEE Sn_RX_RD Sn_RX_WR
1560 POKE &HFF69,&H04 : POKE &HFF6A,&H28 
1570 RP = (PEEK(&HFF6B)*256)+PEEK(&HFF6B)                  ' RP = READ POINTER
1580 RL = (RP AND &H7AAA)+ &H6000                          ' READ LOC=(RP AND MASK+BASE)
1590 EB = (&HE000) 
1600 SG = RZ                                               ' PROPOSED SEG = ALL BYTES
1610 NP = RP+RZ+1                                          ' PROPOSED NEXT READ PTR
1620 IF EB-RL > RZ THEN 1800                               ' SINGLE SEGMENT FITS, JUMP  
1630 REM *** TWO SEGMENT ***
1640 SG = EB-RL                                            ' SEGMENT = REST OF BUFFER
1650 GOSUB 1800                                            ' GET THAT 
1660 SG = RZ-SG                                            ' SEGMENT = REMAINING DATA
1670 RP = &H6000                                           ' RESTART READ POINTER      
1680 B = INT(RP/256) : C = RP-(256*B)
1690 POKE &HFF69,&H04 : POKE &HFF6A,&H28 
1700 POKE &HFF69,B : POKE &HFF6A,C
1710 NP = &H6000+SG+1                                       ' ALSO RESET NEXT READ PTR
1720 REM *** SINGLE SEGMENT ***                                             
1730 GOSUB 1800                                             ' GET REMAINDER OF RZ 
1740 REM *** CLEANUP / EXIT ***                                              
1750 POKE &HFF69,&H04 : POKE &HFF6A,&H28 
1760 B = INT(NP/256) : C = NP-(256*B)
1770 POKE &HFF69,B : POKE &HFF6A,C 
1780 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B,&H40' SIGNAL READ IS DONE 
1790 GOTO 2000
1800 REM *** PRINT DATA ***
1810 B = INT(RL/256) : C = RL-(256*B)                      ' START READ LOC IN 2 BYTES 
1820 POKE &HFF69,B : POKE &HFF6A,C                         ' SET READ ADDR ON WIZNET
1830 FOR I = 1 TO SG
1840 D = PEEK(&HFF6B)
1850 IF D > 32 AND D < 127 THEN PRINT CHR$(D);
1860 NEXT I
1870 RETURN
1880 *** END DATA READ *******************************
2000 REM ****  CONTINUE HERE  ****
10000 REM ******** STATUS DUMP ***********************                 
10010 PRINT "SOCKET OPEN."
11020 GOTO 20070
20030 IF C = 0 THEN PRINT "CONNECT FAILED."
20040 PRINT "SOCKET STATUS = "; HEX$(S); " ";
20050 IF S = 15 THEN PAUSE 3 : GOTO 1300           ' TRY AGAIN
20060 POKE &HFF69,0 : POKE &HFF6A,01    
20070 PRINT
20080 PRINT " GATEWAY"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20090 PRINT "  SUBNET"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20100 PRINT "MAC ADDR "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B));
20110 PRINT " "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B))
20120 PRINT "   MY IP"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20130 PRINT " MY PORT"; PEEK(&HFF6B) * 256 + PEEK(&HFF6B)
20140 PRINT
20150 POKE &HFF69,&H04 : POKE &HFF6A,&H0C
20160 PRINT "  REM IP"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20170 PRINT "REM PORT"; PEEK(&HFF6B) * 256 + PEEK(&HFF6B)
20180 PRINT "<EOF>"
20190 POKE &HFF69,&H00 : POKE &HFF6A,&H01 : POKE &HFF6B,&H00   'CLOSE SOCKET ' 
