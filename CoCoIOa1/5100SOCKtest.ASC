0 REM KEEP THIS LINE FOR TOOLSHED
5 REM *** 4/12/21 DLINK AS ROUTER LAPTOP AS SERVER ***
110 CLS
115 C=1
120 PRINT "** TEST 5100 SOCKET OPEN V0.3 **" 
130 POKE &HFF68,&H80   'RESET 5100
140 POKE &HFF68,&H03   'AUTO:INC, INDIRECT MODE
1100 ' ***** LOCAL CONFIG *****
1110 POKE &HFF69,0 : POKE &HFF6A,01                          ' GLOBAL CONFIG REGS START
1120 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,1 ' GATEWAY
1130 POKE &HFF6B,255 : POKE &HFF6B,255 : POKE &HFF6B,255 : POKE &HFF6B,0 ' SUBNET
1140 POKE &HFF6B,&H5C : POKE &HFF6B,&H26 : POKE &HFF6B,&H0A              ' MAC ADDR PT1
1145 POKE &HFF6B,&H01 : POKE &HFF6B,&H02 : POKE &HFF6B,&H03              ' MAC ADDR PT2
1150 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,7   ' MY IP
1200 ' **** REMOTE CONFIG *****
1210 POKE &HFF69,&H04 : POKE &HFF6A,&H00 : POKE &HFF6B, 1   ' S0 > TCP MODE
1220 POKE &HFF69,&H04 : POKE &HFF6A,&H04                    ' LOCAL PORT REG 
1224 POKE &HFF6B,&HC0 : POKE &HFF6B,&H00                    '  SET EPHM PORT #
1230 POKE &HFF69,&H04 : POKE &HFF6A,&H0C                    ' FOREIGN IP REG
1234 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,1 : POKE &HFF6B,124
1240 ' ADDR VIA AUTO INC                                    ' FOREIGN PORT REG
1244 POKE &HFF6B,0 : POKE &HFF6B,80                         '  SET PORT #
1300 ' ***** OPEN SOCKET *****
1310 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B, 1   ' TRY 2 OPEN SOCKET
1320 POKE &HFF69,&H04 : POKE &HFF6A,&H03 : S = PEEK(&HFF6B) ' GET STATUS
1323 IF S <> &H13 THEN GOTO 20002                           ' STAT <> SOCK_INIT
1330 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B, 4   ' TRY CONNECT   
1340 POKE &HFF69,&H04 : POKE &HFF6A,&H03 : C = PEEK(&HFF6B) ' GET STATUS
1343 IF C = 0 THEN GOTO 20001                               ' STAT = SOCK_CLOSED
1346 IF C <> &H17 THEN 1320                                 ' WAIT FOR SOCK_ESTAB                            
11000 PRINT "SOCKET OPEN."
11010 GOTO 20008
20000 ' ***** STATUS DUMP *****
20001 IF C = 0 THEN PRINT "CONNECT FAILED."
20002 PRINT "SOCKET STATUS = "; HEX$(S); " ";
20004 IF S = 15 THEN PAUSE 3 : GOTO 320
20008 PRINT
20010 POKE &HFF69,&H00 : POKE &HFF6A,&H01
20020 PRINT " GATEWAY"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20040 PRINT "  SUBNET"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20060 PRINT "MAC ADDR "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B));
20065 PRINT " "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B))
20080 PRINT "   MY IP"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20100 POKE &HFF69,&H0 : POKE &HFF6A,&H01 
20102 PRINT " MY PORT"; PEEK(&HFF6B) * 256 + PEEK(&HFF6B)
20104 PRINT
20110 POKE &HFF69,&H04 : POKE &HFF6A,&H0C
20110 PRINT "  REM IP"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20120 PRINT "REM PORT"; PEEK(&HFF6B) * 256 + PEEK(&HFF6B)
20130 PRINT "<EOF>"



